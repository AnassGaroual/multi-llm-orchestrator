plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.7'
    id 'io.spring.dependency-management' version '1.1.7'
}

group = 'com.multi'
version = '0.0.1-SNAPSHOT'
description = 'multi-llm-orchestrator'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(25)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
    testRuntimeClasspath {
    exclude group: 'org.springframework.ai'   // nukes every Spring AI autoconfig in tests
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springAiVersion', "1.0.3")
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.ai:spring-ai-starter-model-chat-memory'
    implementation 'org.springframework.ai:spring-ai-starter-model-mistral-ai'
    implementation 'org.springframework.ai:spring-ai-starter-model-ollama'
    implementation 'org.springframework.ai:spring-ai-starter-model-openai'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.boot:spring-boot-testcontainers'
    testImplementation 'org.springframework.ai:spring-ai-spring-boot-testcontainers'
    testImplementation 'org.springframework.security:spring-security-test'
    testImplementation 'org.testcontainers:junit-jupiter'
    testImplementation 'org.testcontainers:ollama'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.ai:spring-ai-bom:${springAiVersion}"
    }
}

tasks.named('test') {
    useJUnitPlatform()
  doFirst {
    def agent = configurations.testRuntimeClasspath.resolve().find { f ->
      f.name.startsWith('byte-buddy-agent') && f.name.endsWith('.jar')
    }
    if (!agent) {
      throw new GradleException("byte-buddy-agent.jar introuvable dans testRuntimeClasspath (via spring-boot-starter-test).")
    }
    jvmArgs("-javaagent:${agent.absolutePath}")
  }
}

tasks.withType(Test).configureEach {
  jvmArgs "-Xshare:off"
}
tasks.withType(JavaExec).configureEach {
  jvmArgs "-Xshare:off"
}

